apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + gitVersionCode()
    }

    return version
}

android {
    signingConfigs {
        wyz {
            keyAlias 'mzh'
            keyPassword 'ww719010'
            storeFile file('E:/AndroidStudioProjects/wyz.jks')
            storePassword 'ww719010'
        }
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.0'
    //useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.murainy.safeexam"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName '1.0'
        signingConfig signingConfigs.wyz
    }
    buildTypes {
        debug {
            // 为了不和 release 版本冲突
            applicationIdSuffix ".debug"
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.wyz
            //混淆
            minifyEnabled false
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.mergedFlavor.versionCode = gitVersionCode()
            variant.mergedFlavor.versionName = gitVersionTag()

        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
        jcenter()
        maven { url "https://jitpack.io"}

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
        xiaomi {
        }
    }
    sourceSets {
        main() {
            jniLibs.srcDirs = ['libs']
        }
    }
    dexOptions {
        incremental true
    }
}


dependencies {
    compile(name: 'circleimageview-release', ext: 'aar')
    compile(name: 'circle-menu', ext: 'aar')
    compile(name: 'shimmer-android-release', ext: 'aar')
    compile(name: 'welcome-release', ext: 'aar')
    compile(name: 'fancybuttons-release', ext: 'aar')
    compile(name: 'MaterialProgressBar', ext: 'aar')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'com.orhanobut:logger:1.11'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.android.support:support-annotations:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:v1.7.4'
    compile 'com.android.support:design:24.1.1'
    //bmob-sdk
    compile 'cn.bmob.android:bmob-sdk:3.5.0'
}

